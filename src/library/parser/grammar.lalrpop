use crate::library::ast::untyped::*;
use crate::library::parser::get_binop;

grammar;

match {
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"#[^\n\r]*[\n\r]*" => { }, // Skip `# comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    "fun", "val", "type", "from", "import",
    "true" => TRUE,
    "false" => FALSE,
    "if", "else",
    "&&", "||",
    "<", "<=", "!=", "==", ">=", ">",
    ",", "::", ":",
    "(", ")",
    "{", "}",
    "->",
    "=",
    "*", "/", "+", "-",
    ";",
} else {
    r"[a-z_][a-zA-Z_0-9]*" => ID,
    r"[A-Z][a-zA-Z_0-9]*" => TID,
    r"-?[0-9]+" => INT,
    r"-?[0-9]+\.[0-9]*" => FLOAT,
    r#"".*""# => STRING,
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => { v.push(e); v }
    }
};

Arrow<T>: Vec<T> = {
    <mut v:(<T> "->")*> <e:T?> => match e {
        None => v,
        Some(e) => { v.push(e); v }
    }
};

DoubleColon<T>: Vec<T> = {
    <mut v:(<T> "::")*> <e:T?> => match e {
        None => v,
        Some(e) => { v.push(e); v }
    }
};

Id: String =
    ID => <>.to_string();

Tid: String =
    TID => <>.to_string();

Type: Type = {
    <Tid> => Type::Simple(<>),
    "(" <Arrow<Type>> ")" => Type::Function(<>),
};

TypedId: (String, Type) =
    <id:Id> ":" <t:Type> => (id, t);

Lit: Lit = {
    INT => Lit::Int(str::parse(<>).unwrap()),
    FLOAT => Lit::Float(<>.to_string()),
    TRUE => Lit::Bool(true),
    FALSE => Lit::Bool(false),
    STRING => Lit::String(<>.to_string()),
};

Expr0: Expr = {
    "(" <Expr> ")",
};

Expr1: Expr = {
    <Fun> => Expr::Function(Box::new(<>), ()),
    <id:Tid> "{" <args:Comma<Expr>> "}"
        => Expr::New(id, args),
    <Expr0>,
};

Expr2: Expr = {
    <id: Id> "(" <args: Comma<Expr>> ")"
        => Expr::Call(id, args),
    <id:Id> => Expr::Value(id),
    <Lit> => Expr::Lit(<>),
    <Expr1>,
};

Expr3: Expr = {
    <lh:Expr3> "*" <rh:Expr2> => get_binop(Op::Mul)(<>),
    <lh:Expr3> "/" <rh:Expr2> => get_binop(Op::Div)(<>),
    <Expr2>,
};

Expr4: Expr = {
    <lh:Expr4> "+" <rh:Expr3> => get_binop(Op::Add)(<>),
    <lh:Expr4> "-" <rh:Expr3> => get_binop(Op::Sub)(<>),
    <Expr3>,
};

Expr5: Expr = {
    <lh:Expr5> "<" <rh:Expr4> => get_binop(Op::Lt)(<>),
    <lh:Expr5> "<=" <rh:Expr4> => get_binop(Op::Le)(<>),
    <lh:Expr5> "!=" <rh:Expr4> => get_binop(Op::Ne)(<>),
    <lh:Expr5> "==" <rh:Expr4> => get_binop(Op::Eq)(<>),
    <lh:Expr5> ">=" <rh:Expr4> => get_binop(Op::Ge)(<>),
    <lh:Expr5> ">" <rh:Expr4> => get_binop(Op::Gt)(<>),
    <Expr4>,
}

Expr6: Expr = {
    <lh:Expr6> "&&" <rh:Expr5> => get_binop(Op::And)(<>),
    <lh:Expr6> "||" <rh:Expr5> => get_binop(Op::Or)(<>),
    <Expr5>,
}

Expr7: Expr = {
    <Expr6>,
};

Expr8: Expr = {
    "val" <tid:TypedId> "=" <expr:Expr7>
        => Expr::Assign(tid, Box::new(expr)),
    <Expr7>
};

Expr9: Expr = {
    <lh:Expr9> ";" <rh:Expr8>
        => Expr::Chain(Box::new(lh), Box::new(rh)),
    <Expr8>,
};

pub Expr: Expr = {
    "if" <be:Expr> "{" <e1:Expr9> "}" "else" "{" <e2:Expr9> "}"
        => Expr::If(Box::new(be), Box::new(e1), Box::new(e2)),
    <id:Id> "->" <attr:Id>
        => Expr::Attr(id, (), attr),
    <Expr9>,
};

pub Fun: Fun = {
    "fun" <name:Id> "(" <args:Comma<TypedId>> ")" ":" <rt: Type> "{" <body:Expr> "}"
        => Fun {<>},
};

Struct: Struct = {
    "type" <name:Tid> "{" <args:Comma<TypedId>> "}"
        => Struct { name, args },
};

Import: Import = {
    "from" <qualifier:DoubleColon<Id>> "import" <name:Id>
        => (qualifier, name),
    "from" <qualifier:DoubleColon<Id>> "import" <name:Tid>
        => (qualifier, name),
};

pub Decl: Decl = {
    <Fun> => Decl::Fun(<>),
    <Struct> => Decl::Struct(<>),
    <Import> => Decl::Import(<>),
};

pub Mod: Vec<Decl> =
    <Decl*>;
